 ; <html><head><title>Creates ref projections</title></head><body><pre>
 ;
 ; SOURCE: spider/docs/techs/recon1/Procs/make-ref-views.spi
 ;
 ; PURPOSE:  Sets angles for reference projections.
 ;           Creates reference projections from a reference volume.
 ;
 ;
 ; USAGE:    ./spider spi/dat @make-ref-views

 ; <b> ------------ Parameters ---------------------------------------

 [numProcs]   = 0      ; Number of processors to use (0 == All)

 ; INPUT FILES:
 ;   [ref_vol]        ../ref_vol_filt         Current reference volume    (one) 
 ;
 ; OUTPUT FILES: ([init_dir] denotes output directory)
 ;   [iter_refangs]   [init_dir]/ref_angs_00    Reference angles doc file   (one)                  
 ;   [ref_projs]      [init_dir]/ref_projs_00   Reference projections       (one) 
 ;   [ref_view_list]  [init_dir]/sel_proj       View list doc file          (one)
 ; 
 ; -------------- END BATCH HEADER ---------------------------------</b>

 MD
   TR OFF                  ; Decrease results file output
 MD
   VB OFF                  ; Decrease results file output
 MD 
   SET MP 
   [numProcs]
 
 ; Get reconstruction parameters & file names
 @recon-settings([pixsiz],[ang-step],[r2],[alignsh],[prj-radius],[winsiz],[incore-yn],[bp-type],[qsub],[masterCPUs],[groupCPUs])

;; SYS
;;    mkdir -p [rec_dir]

;;; [iter] = 0

;; SYS
;;   mkdir -p [rec_dir]

 DE                        ; Delete 
   [iter_refangs]          ; Reference projection angles doc file   (deleted)
 DE                        ; Delete 
   [ref_view_list]         ; Selection doc file                     (deleted)

 ; Get particle diameter
 UD 18, [sp_partsiz]
   [params]
 UD E
 
 ; Compute projection radius
 [prj-radius] = 0.69 * [sp_partsiz]
 
 ; List angles for reference projections in doc file.
 VO EA [num-angs]          ; Sets [num-ang] to number of reference projections
   [ang-step]              ; Theta angular step            (varies with iteration)
   0, 90                   ; Theta range, 90 is for use with 'Check Mirrored Positions'
   0, 359.9                ; Phi range
   [iter_refangs]          ; Reference projection angles doc file   (output)

![num-angs] = [num-angs] - 1   ; Skip duplicated angle
 [num-angs]  

 ; Create ('PJ 3F') stack holding reference projections. 

 SYS
   echo  '  Creating: {%I0%[num-angs]} reference projections with radius: {%I0%[prj-radius]}' ; echo

 PJ 3F                     ; Projection operation
   [ref_vol]               ; Current reference volume             (input) 
   [prj-radius]            ; Radius of projection
   1-[num-angs]            ; Reference angles used    
   [iter_refangs]          ; Reference angles doc file            (input)
   [ref_projs]@*****       ; Reference projection stack template  (output) 

;; SYS
;;   mkdir -p [avg_dir]

 DOC CREATE
   [ref_view_list]         ; View list doc file    (output)
   1                       ; Column to be filled
   1-[num-angs]            ; Numbers to put in column

 SYS
   echo  '  Created view list doc file: [ref_view_list]' ; echo

 EN 

 ; Modified 2017-07-27
 ;    2016-05-26 (trs) -- gets particle diameter from PARAMS
 ;    2016-02-09 (trs) -- made #CPUs user-defined
 
 ; </pre></body></html>

