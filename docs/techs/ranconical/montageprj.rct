; CREATE MONTAGE OF PROJECTIONS

; -------------- Parameters --------------

rr [projNum]
?projection number?
rr [soln]
?solution number?
rr [num-solutions]
?number of solutions?

; ---------------- Inputs ----------------
fr
?reference projections?[ref_projs]
fr
?initial aligned projections?[initial_projs]
fr
?refined aligned projections?[refine_projs]

; ---------------- Outputs ----------------
fr
?reprojection stack?[reprojection_stack]

; ------------ END BATCH HEADER ------------

;;fr l
;;[temp_blank]_1

; if first solution then initialize
if ([soln] .eq. 1) then
    [sliceNum] = ([projNum]-1)*([num-solutions]+1)*2 + 1
    [sliceNum]

    ; make stack of (0,0,0) projections
    la
    [ref_projs]@{***[projNum]}
    [reprojection_stack]@{****[sliceNum]}
    REF

    ; get dimensions
    fi x [xdim],[ydim]
    [reprojection_stack]@{****[sliceNum]}
    (12,2)   ; header positions

    [sliceNum] = ([projNum]-1)*([num-solutions]+1)*2 + 2
    [sliceNum]

;    bl
;    [temp_blank]
;    [xdim],[xdim]
;    n
;    (0)   ; background

    ; leave blank at 2nd position but with label
    la
    [ref_projs]@{***[projNum]}   ; WAS [temp_blank]
    [reprojection_stack]@{****[sliceNum]}
    PRJ {**[projNum]}
    ; (TO DO: label with Euler angles also)
endif

[sliceNum] = ([projNum]-1)*([num-solutions]+1)*2 + ([soln])*2 + 1
[sliceNum]

la
[initial_projs]@{***[projNum]}
[reprojection_stack]@{****[sliceNum]}
INIT {*[soln]}

[sliceNum] = ([projNum]-1)*([num-solutions]+1)*2 + ([soln])*2 + 2
[sliceNum]

la
[initial_projs]@{***[projNum]}
[reprojection_stack]@{****[sliceNum]}
REFI {*[soln]}

re

; Modified 2016-02-09
;    2016-02-09 (trs) -- repeats reference projection in second spot
;    2015-03-17 (trs & qw) -- bug fix: in output montage, wrote the same solution's re-projections
