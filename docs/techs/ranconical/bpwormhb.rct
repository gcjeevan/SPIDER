;------------------------------------------------------------------------------!
; Adapted from b12.fed/hbl : 3D Reconstruction from the tilted-speci;en images 
;        corresponding to the three image classes obtained from MSA :
;------------------------------------------------------------------------------!

; NOTE:
;   Make sure that the view of the untilted class numbers in this batch file
;   are the ones you want, and that they correspond to the correct symmetry file.
;   For example, untilted class average #4 may not be the end-on view in your
;   own image set

; --------------------- Parameters ---------------------
[multirefIter]   = -1  ; iteration of multireference alignment to use as input (<0==last)
[topViewClass]   = 4   ; class depicting top view
[sideViewClassA] = 1   ; class depicting side view
[sideViewClassB] = 2   ; another class depicting side view
[numProcs]       = 0   ; number of CPUs to use (0==ALL)

; -------------------- Input files --------------------
FR L
[multiref_tracking_doc]Zerodegr/docmultiref            ; list of multireference alignment iterations 
FR L
[class_dir]Zerodegr/Multiref{*[iter]}                  ; input directory
FR L
[class_select_doc][class_dir]/goodsel***   ; class selection doc filename template
FR L
[aligned_tilted]Tilted/stk3-cent@*****                 ; tilted aligned tilted image filename template
FR L
[angles_doc]Tilted/dangles                             ; Euler angle doc file
FR L
[top_sym]Volumes/d6top                                 ; top-view symmetry doc file
FR L
[side_sym]Volumes/d6side                               ; side-view symmetry doc file

; -------------------- Output files --------------------
FR L
[volume_dir]Volumes                            ; output reconstruction directory
FR L
[class_vol][volume_dir]/vcla***   ; class volume filename template

; ---------------------- END BATCH HEADER ----------------------

SYS
mkdir [volume_dir]

; GET ITERATION NUMBER

; if input iteration-specified
IF ( [multirefIter] .GT. 0 ) THEN
    [iter] = [multirefIter]

; if input iteration not specified
ELSE
    ; look for last iteration number
    UD N [lastIterKey]
    [multiref_tracking_doc]
    
    ; get iteration number
    UD [lastIterKey], [iter]
    [multiref_tracking_doc]
    UD E  ; close doc
ENDIF

SYS
echo "Using classification/alignments from [class_dir]"; echo

MD
SET MP
[numProcs]

@bp3f
[sideViewClassA]    ; PARAMETER: class number
[class_select_doc]  ; INPUT: selection doc
[angles_doc]        ; INPUT: angles doc
[aligned_tilted]    ; INPUT: tilted-image template
[side_sym]          ; INPUT: side-view symmetry doc
[class_vol]         ; OUTPUT: reconstruction

@bp3f
[sideViewClassB]    ; PARAMETER: class number
[class_select_doc]  ; INPUT: selection doc
[angles_doc]        ; INPUT: angles doc
[aligned_tilted]    ; INPUT: tilted-image template
[side_sym]          ; INPUT: side-view symmetry doc
[class_vol]         ; OUTPUT: reconstruction

@bp3f
[topViewClass]      ; PARAMETER: class number
[class_select_doc]  ; INPUT: selection doc
[angles_doc]        ; INPUT: angles doc
[aligned_tilted]    ; INPUT: tilted-image template
[top_sym]           ; INPUT: top-view symmetry doc
[class_vol]         ; OUTPUT: reconstruction

SYS
echo "Done" ; date

EN 

; Modified 2018-02-07
;    2015-07-16 (trs) -- separated from general reconstruction procedure, bpclass.rct
;    2014-03-06 (trs) -- reading images from stacks
;    2014-03-04 (trs) -- looks for last iteration of multireference alignment
