; PROJECT REFERENCE VOLUME
;
; Adapted from refproj.spi

; -------------- Parameters --------------

[classNum]     = 1   ; class number to calculate projections (0==all)
[multirefIter] = -1  ; iteration of multireference alignment to use as input (for multiple classes, <0==last)
[dtheta]       = 15  ; angular increment for references (15==83 projections)

; ---------------- Inputs ----------------
FR L
[parameter_doc]params                        ; parameter doc file
FR L
[class_vol]Volumes/vcla***                   ; class volume template
FR L
[multiref_tracking_doc]Zerodegr/docmultiref  ; list of multireference alignment iterations (if reconstructing multiple classes)
FR L
[class_dir]Zerodegr/Multiref{*[iter]}        ; alignment directory (if reconstructing multiple classes)
FR L
[class_list][class_dir]/goodviews            ; list of classes (if reconstructing multiple classes)

; ---------------- Outputs ----------------
FR L
[pocs_dir]Pocs                    ; output directory
FR L
[ref_angles][pocs_dir]/refangles  ; reference-angles doc file
FR L
[list_projs][pocs_dir]/projlist   ; list of reference projections
FR L
[prj_stack]prj_***                ; reference-projection stack (w/o "@"), in POCS_DIR
FR L
[last_prjs]prj-LAST               ; softlink to most recent stack of projections (for SPIRE)
FR L
[power_spectrum]stkpws_***        ; power-spectrum stack, in POCS_DIR
FR L
[last_pws]stkpws-LAST             ; softlink to most recent stack of power spectra (for SPIRE)

; ----------------- END BATCH HEADER -----------------

; Temporary files
[temp_pws]   = '_1'
[temp_stack] = '_2'

; get image-dimension
UD 17, [imgDim]
[parameter_doc]
UD E

SYS
mkdir [pocs_dir]

DE 
[ref_angles]

; compute projection angles
VO EA [numang]
[dtheta]       ; Delta theta
0,0            ; Range of theta
0,0            ; Range of phi
[ref_angles]   ; Doc file of angles       (output)

[numang] = [numang] - 1
; (Last projection is redundant with (0,0,0).)

DOC CREATE
[list_projs]
1           ; column# to fill
1-[numang]  ; values to enter

; check if all classes or single class specified
IF ( [classNum] .EQ. 0 ) THEN
    ; get iteration number

    ; if input iteration-specified
    IF ( [multirefIter] .GT. 0 ) THEN
        [iter] = [multirefIter]

    ; if input iteration not specified
    ELSE
        ; look for last iteration number
        UD N [lastIterKey]
        [multiref_tracking_doc]
        
        ; get iteration number
        UD [lastIterKey], [iter]
        [multiref_tracking_doc]
        UD E  ; close doc
    ENDIF

    ; get #classes
    UD N [numClasses]
    [class_list]
    
    SYS
    echo "Projecting all {***[numClasses]} classes listed in [class_list]" ; echo
ELSE
    [numClasses] = 1
    [currClass]  = [classNum]
ENDIF

[prjRadius] = [imgDim]/2 - 1

; loop through classes
DO LB3 [classKey] = 1,[numClasses]
    ; get class# if not a single class
    IF ([classNum] .EQ. 0) THEN
        UD IC [classKey], [currClass]
        [class_list]
    ENDIF
    ; (If single class, [currClass] will have been set above.)
    
    DE
    [pocs_dir]/[prj_stack][currClass]@
    DE
    [pocs_dir]/[power_spectrum][currClass]@
    
    MS
    [temp_stack]@
    [imgDim],[imgDim]
    [numang]

    SYS
    echo "Projecting [class_vol][currClass]"

    ; project volume
    PJ 3Q
    [class_vol][currClass]                  ; Volume to be projected          (input)
    [prjRadius]                             ; Radius
    1-[numang]                              ; List of file numbers
    [ref_angles]                            ; Proj. angles doc file           (input)
    [pocs_dir]/[prj_stack][currClass]@****  ; Template for projection stack   (output)
    
    ; Loop through projections
    DO LB2 [prjNum] = 1,[numang]
        PW
        [pocs_dir]/[prj_stack][currClass]@{****[prjNum]}
        [temp_pws]
        
        SQRT
        [temp_pws]
        [temp_stack]@{****[prjNum]}
        ; (For some reason, writing directly to disk directly didn't work.)
    LB2
    ; End projection-loop
LB3
; end class-loop

CP
[temp_stack]@
[pocs_dir]/[power_spectrum][currClass]@

; create soft links of most recent stacks for SPIRE
SYS
cd [pocs_dir] && rm -f [last_prjs].$DATEXT && ln -sv [prj_stack][currClass].$DATEXT      [last_prjs].$DATEXT ; echo
SYS
cd [pocs_dir] && rm -f [last_pws].$DATEXT  && ln -sv [power_spectrum][currClass].$DATEXT [last_pws].$DATEXT  ; echo

EN D

; Modified 2018-02-14
;    2016-04-20 (trs) -- creates link to more recent projection stack for SPIRE
;    2015-08-13 (trs) -- writes (square root of) power spectrum of reference projection
