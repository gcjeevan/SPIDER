; GENERATE PARTICLE-LIST FOR EACH MICROGRAPH

; ------------- Input files -------------
FR L
[mic2particle_table]order_picked         ; micrograph-to-particle lookup-table
FR L
[selection_list]listparticles            ; particle list

; ------------- Output files -------------
FR L
[good_dir]Micrographs/good               ; particle-list output directory
FR L
[tot_particle_list][good_dir]/docall***  ; total-particle list
FR L
[good_particle_list][good_dir]/ngood***  ; good-particle list

; ----------- END BATCH HEADER -----------

SYS
mkdir -p [good_dir]

; get #micrograph-pairs
UD N [numPairs]
[mic2particle_table]

; loop through micrographs
DO LB1 [micKey] = 1,[numPairs]
    ; get micrograph#, first & last particle #s
    UD IC [micKey], [mic],x99,x99,[firstPart],[lastPart]
    [mic2particle_table]
    
    ; create a list of all particles
    DOC CREATE
    [tot_particle_list][mic]
    1  ; column# to fill
    [firstPart]-[lastPart]
    
    ; intersect with total particle list
    DOC AND
    [selection_list]
    [tot_particle_list][mic]
    [good_particle_list][mic]_unrenum
    1  ; column# to intersect

    DOC REN
    [good_particle_list][mic]_unrenum
    [good_particle_list][mic]

    DE
    [good_particle_list][mic]_unrenum
LB1
; end micrograph-loop

EN D

; Modified 2018-02-07
