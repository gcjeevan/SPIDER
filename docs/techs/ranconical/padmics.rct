; PAD MICROGRAPH PAIRS TO THE SAME DIMENSIONS FOR USE IN JWEB

; --------------- Inputs ---------------
fr l
[mic_pair_doc]docmicpairs               ; micrograph-pairing list
fr l
[small_micros]Micrographs/sm-mic***     ; reduced micrographs

; --------------- Output ---------------
fr l
[padded_micros]Micrographs/jweb-mic***   ; reduced micrographs

; ---------- END BATCH HEADER ----------

; get #pairs
ud n [num-pairs]
[mic_pair_doc]

; loop through pairs
do lb1 x10=1,[num-pairs]
    ; get untilted & tilted micrograph#
    ud ic x10,[untilted-num],[tilted-num]
    [mic_pair_doc]

    vm
    echo "Working on micrographs {****[untilted-num]} and {****[tilted-num]}, {***x10}th pair out of {***[num-pairs]}"

    ; get untilted micrograph dimensions
    fi [xdim-untilted],[ydim-untilted]
    [small_micros][untilted-num]
    (12,2)

    ; get tilted micrograph dimensions
    fi [xdim-tilted],[ydim-tilted]
    [small_micros][tilted-num]
    (12,2)


    ; GET LARGER OF TWO DIMENSIONS

    ; xdim
    if([xdim-untilted].gt.[xdim-tilted]) then
        [new-xdim] = [xdim-untilted]
    else
        [new-xdim] = [xdim-tilted]
    endif

    ; ydim
    if([ydim-untilted].gt.[ydim-tilted]) then
        [new-ydim] = [ydim-untilted]
    else
        [new-ydim] = [ydim-tilted]
    endif

    ; pad untilted micrograph to new dimensions
    pd
    [small_micros][untilted-num]
    [padded_micros][untilted-num]
    [new-xdim],[new-ydim]
    Y      ; background to average?
    (1,1)  ; top-left coords

    ; pad tilted micrograph to new dimensions
    pd
    [small_micros][tilted-num]
    [padded_micros][untilted-num]_tilted
    [new-xdim],[new-ydim]
    Y      ; background to average?
    (1,1)  ; top-left coords
    ; (naming scheme also places tilted micrograph 2nd for JWEB's benefit)
lb1
; end pair-loop

ud ice  ; close doc
[mic_pair_doc]

vm
echo "Done" ; date


en d

; Modified 2015-09-10
 
