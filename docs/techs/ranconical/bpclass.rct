;------------------------------------------------------------------------------!
; Adapted from b12.fed/hbl : 3D Reconstruction from the tilted-speci;en images 
;        corresponding to the three image classes obtained from MSA :
;------------------------------------------------------------------------------!

; NOTE:
;   Make sure that the view of the untilted class numbers in this batch file
;   are the ones you want, and that they correspond to the correct symmetry file.
;   For example, untilted class average #4 may not be the end-on view in your
;   own image set

; --------------------- Parameters ---------------------
[singleClass]  = 0   ; class number to calculate reconstruction (0==all)
[multirefIter] = -1  ; iteration of multireference alignment to use as input (<0==last)
[flipflopYN]   = 0   ; compute reconstructions for "flip" and "noflip" subclasses?
[numProcs]     = 0   ; number of CPUs to use (0==ALL)

; -------------------- Input files --------------------
FR L
[class_dir]Zerodegr/Multiref{*[iter]}         ; input directory
FR L
[class_list][class_dir]/goodviews             ; list of classes (if reconstructing multiple classes)
FR L
[class_select_doc][class_dir]/goodsel***      ; class selection doc filename template
FR L
[multiref_tracking_doc]Zerodegr/docmultiref   ; list of multireference alignment iterations 
FR L
[aligned_tilted]Tilted/stk3-cent@*****        ; tilted aligned tilted image filename template
FR ;
[angles_doc]Tilted/dangles                    ; Euler angle doc file
FR L
[symmetry_doc]*                               ; no symmetry doc file

; -------------------- Output files --------------------
FR L
[volume_dir]Volumes                            ; output reconstruction directory
FR L
[class_vol][volume_dir]/vcla***                ; class volume filename template

; ---------------------- END BATCH HEADER ----------------------

SYS
mkdir -p [volume_dir]


; GET ITERATION NUMBER

; if input iteration-specified
IF ( [multirefIter] .GT. 0 ) THEN
    [iter] = [multirefIter]

; if input iteration not specified
ELSE
    ; look for last iteration number
    UD N [lastIterKey]
    [multiref_tracking_doc]
    
    ; get iteration number
    UD [lastIterKey], [iter]
    [multiref_tracking_doc]
    UD E  ; close doc
ENDIF

SYS
echo "Using classification/alignements from [class_dir]"


; check if all classes or single class specified
IF ( [singleClass] .LE. 0 ) THEN
    ; get #classes
    UD N [numClasses]
    [class_list]
    
    SYS
    echo "Reconstructing all {***[numClasses]} classes listed in [class_list]" ; echo
ELSE
    SYS
    echo "Reconstructing only class #{***[singleClass]}" ; echo
    
    [numClasses] = 1
    [classNum]   = [singleClass]
ENDIF

MD
SET MP
[numProcs]

; loop through classes
DO LB1 [classKey] = 1,[numClasses]
    ; get class# if not a single class
    IF ( [singleClass] .LE. 0 ) THEN
        UD IC [classKey], [classNum]
        [class_list]
    ENDIF
    
    @bp3f
    [classNum]          ; PARAMETER: class number
    [class_select_doc]  ; INPUT: selection doc
    [angles_doc]        ; INPUT: angles doc
    [aligned_tilted]    ; INPUT: tilted-image template
    [symmetry_doc]      ; INPUT: symmetry doc (optional)
    [class_vol]         ; OUTPUT: reconstruction
    
    ; check for flip+noflip files
    IQ FI [flipflopExists]
    [class_select_doc]-flip
    
    ; if it exists...
    IF ( [flipflopExists] .EQ. 1 ) THEN
        ; if requested, compute reconstructions
        IF ( [flipflopYN] .EQ. 1 ) THEN
            @bp3f
            [classNum]                 ; PARAMETER: class number
            [class_select_doc]-noflip   ; INPUT: selection doc
            [angles_doc]                ; INPUT: angles doc
            [aligned_tilted]            ; INPUT: tilted-image template
            [symmetry_doc]              ; INPUT: symmetry doc (optional)
            [class_vol]-noflip          ; OUTPUT: reconstruction
            
            @bp3f
            [classNum]               ; PARAMETER: class number
            [class_select_doc]-flip   ; INPUT: selection doc
            [angles_doc]              ; INPUT: angles doc
            [aligned_tilted]          ; INPUT: tilted-image template
            [symmetry_doc]            ; INPUT: symmetry doc (optional)
            [class_vol]-flip          ; OUTPUT: reconstruction
        ELSE
            SYS
            echo "Found [class_select_doc]-flip but reconstruction not requested." ; echo
        ENDIF
    ENDIF
LB1
; end class-loop

SYS
echo ; echo "Done" ; date

EN D

; Modified 2018-02-07
;    2015-07-16 (trs) -- separated from reconstruction procedure for worm hemoglobin, bpwormhb.rct
;    2014-03-06 (trs) -- reading images from stacks
;    2014-03-04 (trs) -- looks for last iteration of multireference alignment
