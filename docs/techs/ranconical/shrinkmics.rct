; SHRINKS MICROGRAPHS

; ------------ Parameters ------------
[progressInt]  = 2              ; progress message displayed every Nth micrograph
[shrinkFactor] = 5              ; downsampling factor
[invertYN]     = 0              ; reverse contrast? (1==yes)
[hiPassStop]   = 2500           ; high-pass (Butterworth) stop band radius, Angstroms (0==none)
[hiPassPass]   = 1000           ; high-pass pass band radius, Angstroms
[loPassPass]   = 30             ; low-pass (Butterworth) pass-band radius, Angstroms (0==none)
[loPassStop]   = 20             ; low-pass stop-band radius, Angstroms

; -------------- Inputs --------------
FR L
[parameter_doc]params                 ; parameter doc file
FR L
[mic_list]filenums                    ; micrograph list
FR L
[input_micros]Micrographs/mic***      ; micrographs

; -------------- Output --------------
FR L
[small_micros]Micrographs/sm-mic***   ; reduced micrographs

; --------- END BATCH HEADER ---------

; get pixel size
UD 5,[pixelSize]
[parameter_doc]
UD e

; convert filter parameters to px^-1
IF([hiPassStop].GT.0) THEN
    [hiPassLo] = [pixelSize]*[shrinkFactor]/[hiPassStop]
    [hiPassHi] = [pixelSize]*[shrinkFactor]/[hiPassPass]
ENDIF

IF([loPassPass].GT.0) THEN
    [loPassLo] = [pixelSize]*[shrinkFactor]/[loPassPass]
    [loPassHi] = [pixelSize]*[shrinkFactor]/[loPassStop]
ENDIF

; get #micrographs
UD N [numMics]
[mic_list]

SYS
echo "Shrinking micrographs by a factor of {%i2%[shrinkFactor]}"; date

IF([invertYN].GE.1) THEN
    SYS
    echo ; echo "Reversing contrast" ; echo
ENDIF

; loop through micrographs
DO lb1 [index] = 1,[numMics]
    ; get micrograph#
    UD IC [index], [micNum]
    [mic_list]

    IF (int([index]/[progressInt]).EQ.[index]/[progressInt]) THEN
        SYS
        echo "Working on micrograph #{****[micNum]}, {****[index]} out of {****[numMics]}"
    ENDIF

    ; reverse contrast if necessary
    IF([invertYN].GE.1) THEN
        NEG
        [input_micros][micNum]
        _2  ; OUTPUT: inverted image
    ELSE
        cp
        [input_micros][micNum]
        _2  ; OUTPUT
    ENDIF

    ; shrink image
    DC S
    _2  ; WAS [input_micros][micNum]
    _1  ; OUTPUT: shrunken image  ; WAS [small_micros][micNum]
    [shrinkFactor],[shrinkFactor]

    IF([hiPassStop].GT.0) then
        ; high-pass filter
        FQ NP
        _1   ; INPUT: shrunken image
        _2   ; WAS [small_micros][micNum]
        (8)  ; filter type: Butterworth high-pass
        [hiPassLo],[hiPassHi]
    ELSE
        cp
        _1   ; INPUT: shrunken image
        _2   ; WAS [small_micros][micNum]
    ENDIF

    IF([loPassPass].GT.0) THEN
        ; high-pass filter
        FQ NP
        _2   ; INPUT: shrunken image
        [small_micros][micNum]
        (7)  ; filter type: Butterworth low-pass
        [loPassLo],[loPassHi]
    ELSE
        cp
        _2   ; INPUT: shrunken image
        [small_micros][micNum]
    ENDIF
LB1
; end micrograph-loop

UD ICE
[mic_list]

SYS
echo "Done"; date

EN D

; Modified 2017-05-31
;    2017-05-31 (trs) -- added low-pass option, changed to Butterworth filter
;    2014-02-14 (trs) -- added option to reverse contrast
;    2014-02-14 (trs) -- bug fix for unfiltered case
